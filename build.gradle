buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.0'

    }
}

plugins {
  id "com.google.protobuf" version "0.8.0"
  id "java"
  id "application"
  id "idea"
}

group 'cacheService'
version '1.0-SNAPSHOT'


protobuf {
    protoc {
        // The version of protoc must match protobuf-java. If you don't depend on
        // protobuf-java directly, you will be transitively depending on the
        // protobuf-java version that grpc depends on.
        artifact = "com.google.protobuf:protoc:3.1.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.0.2'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'io.grpc:grpc-all:1.0.2'
    compile 'ca.umontreal.iro.simul:ssj:+'
    compile group: 'org.apache.commons', name: 'commons-math3', version: '3.1'

}

idea {
    module {
        sourceDirs += file("${projectDir}/src/generated/main/java");
        sourceDirs += file("${projectDir}/src/generated/main/grpc");
    }
}

task runServer(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'cacheService.CacheServiceServer'
}

task runClient(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'cacheService.CacheServiceClient'
}

task runAlgoritmo(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'cacheAlgorithm.AlgoritmoImp'
}

startScripts.enabled = false

task CacheServiceServer(type: CreateStartScripts) {
    mainClassName = "cacheService.CacheServiceServer"
    applicationName = "cache-service-server"
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

task CacheServiceClient(type: CreateStartScripts) {
    mainClassName = "cacheService.CacheServiceClient"
    applicationName = "cache-service-client"
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

task CacheServiceAlgoritmo(type: CreateStartScripts) {
    mainClassName = "cacheAlgorithm.AlgoritmoImp"
    applicationName = "cache-algoritmo"
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into("bin") {
    from(CacheServiceServer)
    from(CacheServiceClient)
    from(CacheServiceAlgoritmo)
    fileMode = 0755
}
